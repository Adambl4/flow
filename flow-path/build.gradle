import com.android.builder.core.BuilderConstants;

buildscript {
  dependencies {
    classpath deps.android_gradle_plugin
  }
}

apply plugin: 'com.android.library'
android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    versionName VERSION_NAME
  }
}

dependencies {
  compile project(':flow')
  testCompile 'junit:junit:4.10'
  // Note: Only assertj 1.x works on Android. 2.x and higher rely on JRE 7 classes.
  testCompile 'org.assertj:assertj-core:1.7.1'
  testCompile 'org.robolectric:robolectric:3.0'

  // Makes travis tolerate robolectric
  // http://www.scriptscoop.net/t/16bc09ba348e/noclassdeffounderror-when-trying-to-test-android-app-with-gradle-robol.html
  // https://issues.gradle.org/browse/GRADLE-3243
  testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
}


android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  if (!BuilderConstants.DEBUG.equals(name)) {
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
  }
}

if (hasProperty("POM_DEVELOPER_ID")) {
  apply from: '../gradle-mvn-push.gradle'
}
